title: Muammer Aksoy
is-pinned-card: true
tags: advisor, left_kemalists
card-image: img/portraits/MuammerAksoy.jpg
view-if: aksoy_advisor = 1
new-page: true 

= Muammer Aksoy

The chief of the technocratic delegation behind the 1960 Constitution, Aksoy is an author and lawyer. In favor of stronger ties with the Western Europe, he is in contact with the European Council, having served as its Turkish delegate in the past.

- @internationalaffairs: Aksoy can manage our international relations with fellow social democratic parties.
- @europe: Aksoy's delegation in the European Council can help us improve relations.
- @AET: This seems like a long shot but we can attempt to join the AET(European Economic Community).
- @root: Return to main

@internationalaffairs
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 3;
go-to: international_relations

@europe
choose-if: advisor_action_timer = 0
unavailable-subtitle: [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 3; {!
if (Q.cyprus_problem == 0 || Q.cyprus_problem == 1 || Q.cyprus_problem == 2) {
    Q.west_relation += 5;
}
!}

[? if cyprus_problem == 0 or cyprus_problem == 1 or cyprus_problem == 2 : We have slightly improved our ties with the western partners. ?]

[? if cyprus_problem == 3 : The dispute over Cyprus and our operation has rendered any diplomatic breakthroughs improbable. ?]

@AET
choose-if: CHP_in_government = 1 and advisor_action_timer = 0
unavailable-subtitle: CHP is not in government. [? if advisor_action_timer > 0 : [+ advisor_action_timer +] months before next advisor action. ?]
on-arrival: advisor_action_timer = 3;
view-if: west_relation > 80

We submit our formal application. Joining AET will have great benefits but a lot of reforms are needed to be made.
